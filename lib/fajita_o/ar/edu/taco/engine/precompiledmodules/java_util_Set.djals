//
// TACO: Translation of Annotated COde
// Copyright (c) 2010 Universidad de Buenos Aires
//
// This program is free software; you can redistribute it and/or
// modify it under the terms of the GNU General Public License
// as published by the Free Software Foundation; either version 2
// of the License, or (at your option) any later version.
//
// This program is distributed in the hope that it will be useful,
// but WITHOUT ANY WARRANTY; without even the implied warranty of
// MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
// GNU General Public License for more details.
//
// You should have received a copy of the GNU General Public License
// along with this program; if not, write to the Free Software
// Foundation, Inc., 51 Franklin Street, Fifth Floor, Boston, MA,
// 02110-1301, USA

//-------------- java_util_Set --------------//
module java_util_Set
sig java_util_Set extends java_lang_Object {}
{}
field java_util_Set_elems:(java_util_Set) -> (java_lang_Object+null) {}


program java_util_Set::contains[
  var thiz:java_util_Set,
  var throw:java_lang_Throwable+null,
  var return:boolean,
  var e:java_lang_Object+null]

Implementation 
{
  throw:=null;
  return:=fun_set_contains[thiz.java_util_Set_elems,e];
}


program java_util_Set::remove[
  var thiz:java_util_Set,
  var throw:java_lang_Throwable+null,
  var return:boolean,  
  var e:java_lang_Object+null]
  
Implementation 
{
  throw:=null;
  if pred_in[e,thiz.java_util_Set_elems] {
    java_util_Set_elems:=fun_rel_difference[ java_util_Set_elems , thiz, e];
    return:=true;
  } else {
    return:=false;
  };
}


program java_util_HashSet::Constructor[
  var thiz:java_util_Set,
  var throw:java_lang_Throwable+null] 
Implementation 

   {
      throw:=null;
      assume pred_empty_set[thiz.java_util_Set_elems];
   
   }


program java_util_Set::add[
  var thiz:java_util_Set,
  var throw:java_lang_Throwable+null,
  var return:boolean,  
  var e:java_lang_Object+null]

Implementation 
{
  throw:=null;
  if pred_in[e,thiz.java_util_Set_elems] {
    return:=false;
  } else {
    java_util_Set_elems:= fun_rel_add [ java_util_Set_elems , thiz, e];
    return:=true;
  };
}

program java_util_Set::size[
  var thiz:java_util_Set,
  var throw:java_lang_Throwable+null,
  var return:JavaPrimitiveIntegerValue]
Specification 
{
    SpecCase #0 {
        ensures {
            equ[throw',null]
            and equ[return',fun_java_primitive_integer_value_size_of[thiz.java_util_Set_elems]]
        }
    }
}
Implementation 
{
  throw:=null;
  return:=fun_java_primitive_integer_value_size_of[thiz.java_util_Set_elems];
}


program java_util_Set::iterator[var thiz:java_util_Set+null,
 				       var throw:java_lang_Throwable+null,
				       var return:java_util_Iterator+null]

Implementation
{
  throw:=null;
  createObject<java_util_Iterator>[return];
  return.Iterator_contains := thiz.java_util_Set_elems;
}
